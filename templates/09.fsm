Value Filldown sysname (\S+)
Value Filldown model ([^:\b]+)
Value source_ip (\S+)
Value l3vpn (\S+)
Value service_id (\d+)
Value service_name ([^"]+)
Value customer_id (\S+)
Value interface (\S+)
Value spoke_spd_id (\S+)
Value spoke_svc_id (\S+)
Value sap (\S+)
Value ingress_qos_id (\S+)
Value egress_qos_id (\S+)
Value sdp_id (\S+)
Value far_end_system_ip (\S+)
Value port (\S+)
Value policy (\S+)
Value lag_id (\S+)
Value port_lag (\S+)
Value card (\S+)
Value buffer_min (\S+)
Value buffer_max (\S+)
Value shutdown (\S+)
Value resv_min (\S+)
Value resv_max (\S+)
Value port_description (.+)

Start
  ^.+ -oCheckHostIP -> Source
  ^.+ show chassis -> Chassis
  ^\s{4}port ${port} -> Port
  ^\s{4}card ${card} -> Card
  ^\s{4}service -> Service
  ^Lag-id\s{14}:\s+${lag_id} -> Lag

Source
  ^.+@${source_ip}'s\s+password: -> Start

Chassis
  ^\s+Name\s+:\s${sysname}
  ^\s+Type\s+:\s${model} -> Start

Service
  ^\s{8}sdp ${sdp_id} mpls create -> Sdp
  ^\s{8}${l3vpn} ${service_id} name "${service_name}" customer ${customer_id} create -> Vpn
  ^\s{4}exit -> Start

Sdp
  ^\s{12}far-end ${far_end_system_ip} -> Record
  ^\s{8}exit -> Service

Vpn
  # ^\s{12}spoke-sdp ${spoke_spd_id}:${spoke_svc_id} -> Record
  ^\s{12}sap ${sap} create -> Sap
  ^\s{12}interface "${interface}" create -> Interface
  ^\s{8}exit -> Service

Interface
  ^\s{16}spoke-sdp ${spoke_spd_id}:${spoke_svc_id} -> Record
  ^\s{12}exit -> Vpn

Sap
  ^\s{16}ingress -> Ingress
  ^\s{16}egress -> Egress
  ^\s{12}exit -> Record Vpn

Ingress
  ^\s{20}qos ${ingress_qos_id}
  ^\s{16}exit -> Sap

Egress
  ^\s{20}qos ${egress_qos_id}
  ^\s{16}exit -> Sap

Port
  ^\s{8}description "${port_description}"
  ^\s{8}ethernet -> Ethernet
  ^\s{4}exit -> Record Start

Ethernet
  ^\s{12}egress-scheduler-policy "${policy}"
  ^\s{8}exit -> Port

Lag
  ^${port_lag}.{36}yes -> Record Start

Card
  ^\s{8}fp 1 -> Fp
  ^\s{4}exit -> Record Start

Fp
  ^\s{12}egress -> CardEgress
  ^\s{8}exit -> Card

CardEgress
  ^\s{16}wred-queue-control -> Wred
  ^\s{12}exit -> Fp

Wred
  ^\s{20}buffer-allocation min ${buffer_min} max ${buffer_max}
  ^\s{20}resv-cbs min ${resv_min} max ${resv_max}
  ^\s{20}${shutdown} shutdown
  ^\s{16}exit -> CardEgress

EOF
