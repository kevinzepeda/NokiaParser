Value Filldown sysname (\S+)
Value Filldown model ([^:\b]+)
Value address ([\d\.\/]+)
Value source_ip (\S+)
Value interface (\S+)
Value port ([^:\s]+)
Value slope_policy (\S+)
Value queue_policy (\S+)
Value qos (\d+)
Value queue_group_name (\S+)
Value isis (\S+)
Value isis_if (\S+)
Value mpls (\S+)
Value rsvp (\S+)
Value lag_id (\S+)
Value port_lag (\S+)
Value egress_policy (\S+)
Value type_port (\S+)
Value type (\S+)
Value queue_group (\S+)
Value bgp_group_name (\S+)
Value bgp_neighbor (.+)
Value port_description (.+)

Start
  ^.+ -oCheckHostIP -> Source
  ^.+ show chassis -> Chassis
  ^\s{4}router Base -> Base
  ^\s{4}port ${port} -> Ports
  ^Lag-id\s{14}:\s+${lag_id} -> Lag
  ^.+show port -> PortType

Source
  ^.+@${source_ip}'s\s+password: -> Start

Chassis
  ^\s+Name\s+:\s${sysname}
  ^\s+Type\s+:\s${model} -> Start

Base
  ^\s{8}interface "${interface}" -> Interface
  ^\s{8}isis ${isis} -> Isis
  ^\s{8}mpls -> Mpls
  ^\s{8}rsvp -> Rsvp
  ^\s{8}bgp -> Bgp
  ^\s{4}exit -> Start

Interface
  ^\s{12}address ${address}
  ^\s{12}port ${port}
  ^\s{12}qos ${qos} egress-port-redirect-group "${queue_group_name}" egress-instance 1
  ^\s{8}exit -> Record Base

Bgp
  ^\s{12}group "${bgp_group_name}" -> Neighbor
  ^\s{8}exit -> Base

Neighbor
  ^\s{16}neighbor ${bgp_neighbor} -> Record
  ^\s{12}exit -> Bgp

Isis
  ^\s{12}interface "${isis_if}" -> Record
  ^\s{8}exit -> Base

Mpls
  ^\s{12}interface "${mpls}" -> Record
  ^\s{8}exit -> Base

Rsvp
  ^\s{12}interface "${rsvp}" -> Record
  ^\s{8}exit -> Base

Ports
  ^\s{8}description "${port_description}"
  ^\s{8}network -> PortNetwork
  ^\s{8}ethernet -> PortEthernet
  ^\s{4}exit -> Record Start

PortNetwork
  ^\s{20}slope-policy "${slope_policy}"
  ^\s{12}exit -> Ports

PortEthernet
  ^\s{12}egress-scheduler-policy "${egress_policy}"
  ^\s{12}network -> PortEthernetNetwork
  ^\s{8}exit -> Ports

PortEthernetNetwork
  ^\s{16}queue-policy "${queue_policy}"
  ^\s{20}queue-group "${queue_group}" instance 1 create
  ^\s{12}exit -> PortEthernet

Lag
  ^${port_lag}.+yes -> Record Start

PortType
  ^${type_port}\s*Up.{42}${type} -> Record
  ^LAG Details -> Start

EOF
