Value Filldown sysname (\S+)
Value Filldown model ([^:\b]+)
Value source_ip (\S+)
Value l3vpn (\S+)
Value service_id (\d+)
Value service_name ([^"]+)
Value customer_id (\S+)
Value interface (.+)
Value sap (\S+)
Value ingress_qos_id (\S+)
Value egress_qos_id (\S+)
Value port (\S+)
Value port_address (.+)
Value policy (\S+)
Value lag_id (\S+)
Value port_lag (\S+)
Value card (\S+)
Value buffer_min (\S+)
Value buffer_max (\S+)
Value shutdown (\S+)
Value resv_min (\S+)
Value resv_max (\S+)
Value bgp_group_name (\S+)
Value bgp_neighbor (\S+)

Start
  ^.+ -oCheckHostIP -> Source
  ^.+ show chassis -> Chassis
  ^\s{4}port ${port} -> Port
  ^\s{4}card ${card} -> Card
  ^\s{4}service -> Service
  ^Lag-id\s{14}:\s+${lag_id} -> Lag

Source
  ^.+@${source_ip}'s\s+password: -> Start

Chassis
  ^\s+Name\s+:\s${sysname}
  ^\s+Type\s+:\s${model} -> Start

Port
  ^\s{8}description "${port_address}"
  ^\s{8}ethernet -> Ethernet
  ^\s{4}exit -> Record Start

Ethernet
  ^\s{12}egress-scheduler-policy "${policy}"
  ^\s{8}exit -> Port

Lag
  ^${port_lag}.{36}yes -> Record Start

Service
  ^\s{8}${l3vpn} ${service_id} name "${service_name}" customer ${customer_id} create -> Vpn
  ^\s{4}exit -> Start

Vpn
  ^\s{12}interface "${interface}" create -> Interface
  ^\s{12}bgp -> Bgp
  ^\s{8}exit -> Service

Bgp
  ^\s{16}group "${bgp_group_name}" -> Neighbor
  ^\s{12}exit -> Vpn

Neighbor
  ^\s{20}neighbor ${bgp_neighbor} -> Record
  ^\s{16}exit -> Bgp

Interface
  ^\s{16}sap ${sap} create -> Sap
  ^\s{12}exit -> Vpn

Sap
  ^\s{20}ingress -> Ingress
  ^\s{20}egress -> Egress
  ^\s{16}exit -> Interface

Ingress
  ^\s{24}qos ${ingress_qos_id}
  ^\s{20}exit -> Sap

Egress
  ^\s{24}qos ${egress_qos_id}
  ^\s{20}exit -> Sap

Card
  ^\s{8}fp 1 -> Fp
  ^\s{4}exit -> Record Start

Fp
  ^\s{12}egress -> CardEgress
  ^\s{8}exit -> Card

CardEgress
  ^\s{16}wred-queue-control -> Wred
  ^\s{12}exit -> Fp

Wred
  ^\s{20}buffer-allocation min ${buffer_min} max ${buffer_max}
  ^\s{20}resv-cbs min ${resv_min} max ${resv_max}
  ^\s{20}${shutdown} shutdown
  ^\s{16}exit -> CardEgress

EOF
# port <port-id>|ethernet|egress-scheduler-policy PS-IPMEAN-access:Mobile||||||
